namespace cms_controller
include std/convert.e
include std/net/url.e
include std/map.e
include std/text.e
include lib/blog/article.exu
include lib/blog/template.exu
include lib/blog/web/auth.exu
include lib/blog/web/response.exu

global function write_article(map params)
    object user_name = auth:check()
    if atom(user_name) then
        return response:create_redirect("/cms/login_form/")
    end if

    object body = map:get(params, "body")
    object title = map:get(params, "title")
    if (length(text:trim(title)) < 1) then
        return response:create_redirect("/cms/?error=title")
    elsif (length(text:trim(body)) < 1) then
        return response:create_redirect("/cms/?error=body")
    end if
    object auto_break = map:get(params, "auto_break")
    if atom(auto_break) then
        auto_break = 0
    else
        auto_break = 1
    end if

    article:write(title, body, auto_break)
    sequence ids = article:get_all_ids()

    map template_params = new_from_kvpairs({
        {"article_number", sprintf("%d", ids[length(ids)])},
        {"page_title", "記事作成完了 - Blog"}
    })

    return response:create_ok(template:process("view/cms/write_done.html", template_params))
end function

global function write_form(map params)
    object user_name = auth:check()
    if atom(user_name) then
        return response:create_redirect("/cms/login_form/")
    end if

    sequence error = ""
    switch map:get(params, "error") do
        case "title" then
            error = "タイトルが未入力です。"
        case "body" then
            error = "本文が未入力です。"
    end switch

    map template_params = new_from_kvpairs({
        {"error", error},
        {"page_title", "記事作成フォーム - Blog"},
        {"user_name", user_name}
    })

    return response:create_ok(template:process("view/cms/writebox.html", template_params))
end function

global function login_user(map params)
    object session_data = auth:login(
        text:trim(map:get(params, "user_name")), text:trim(map:get(params, "password")))
    if atom(session_data) then
        return response:create_redirect("/cms/login_form/?error=1")
    end if

    sequence session_key = session_data[1]
    sequence session_expire = session_data[2]

    map template_params = new_from_kvpairs({
        {"page_title", "ログイン完了 - Blog"}
    })

    return response:create_with_cookie(template:process("view/cms/login_done.html", template_params),
        sprintf("session=%s; expires=%s; path=/", {session_key, session_expire}))
end function

global function login_form(map params)
    sequence error = ""
    if sequence(map:get(params, "error")) then
        error = "ログインに失敗しました。"
    end if

    map template_params = new_from_kvpairs({
        {"error", error},
        {"page_title", "ログインフォーム - Blog"}
    })

    return response:create_ok(template:process("view/cms/login_form.html", template_params))
end function
