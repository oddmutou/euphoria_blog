namespace main_controller
include std/convert.e
include std/map.e
include lib/blog/article.exu
include lib/blog/template.exu

global procedure view(map params)
    atom id = convert:to_integer(map:get(params, "id"))
    object data = article:get_from_id(id)

    map template_params = new_from_kvpairs({
        {"title", data[1]},
        {"body", data[2]}
    })

    puts(1, "Content-type: text/html\n\n")
    puts(1, template:process("view/main/article.html", template_params))
end procedure

global procedure index(map params)
    sequence ids = article:get_all_ids()

    sequence article_list = ""
    for i = 1 to length(ids) do
        atom id = ids[i]
        article_list = article_list
            & sprintf("<li><a href=\"/article/?id=%d\">No: %d</a></li>\n", {id, id})
    end for

    map template_params = new_from_kvpairs({
        {"article_list", article_list}
    })

    puts(1, "Content-type: text/html\n\n")
    puts(1, template:process("view/main/index.html", template_params))
end procedure
